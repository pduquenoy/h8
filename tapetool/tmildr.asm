	TITLE	'Tape Memory Image Loader'

	XTEXT	HOSEQU
	XTEXT   HOSDEF
	XTEXT	ECDEF
	XTEXT	TYPTX
	XTEXT	ESVAL
	XTEXT	MOVE

$CDEHL  EQU     030216A

DLY	EQU	53A		WASTE TIME
SAVALL	EQU	132A		SAVE USR REGISTERS
ERROR	EQU	322A		PAM8 MONITOR RESET ENTRY
HORN	EQU	2140A		MAKE BEEP
LRA.	EQU	3052A		FIND FAKE REGISTERS
START	EQU	40000A		FIRST LOCATION IN A DUMP
.MFLAG	EQU	40010A		OPTION CONTROL LOCATION
ABUSS	EQU	40024A		LAST LOCATION IN DUMP

	ORG	USERFWA

ST	EQU	*

*	LXI	H,02B4H
*	MVI	A,0E9H
*	MOV	M,A

	LXI	H,0
	DAD	SP		GET STACK ADR
	LXI	D,USERFWA	WHERE IT SHOULD BE
	LXI	B,NAME		WILL STORE NAME HERE
NXBYT	CALL	$CDEHL		ANY TYPE-INS?
	JZ	GOTNAM		DONE.
	MOV	A,M		GET A BYTE
	INX	H
	CPI	' '		A SPACE?
	JZ	NXBYT		YES, DISCARD
	CPI	'*'		ASTERIX?
	JNZ	STOR		NO, SAVE CHR
	MVI	A,-1		FLAG ON
	STA	STFLG		REMEMBER NOT TO RUN
	JMP	NXBYT		GET MORE
*
STOR	STAX	B		SAVE IT
	INX	B
	JMP	NXBYT		& GET MORE

GOTNAM	XRA	A		CHANNEL 0
	STAX	B		ALSO DELIMIT NAME
	LXI	D,DEFALT
	LXI	H,NAME
	SCALL	.OPENR		WANT TO READ IT
	JC	RBAD
	LXI	H,-1		WANT THE WORLD
	SCALL	.SETTP		WON'T GET IT
	SCALL	.SETTP		BUT WILL GET MAX
	JC	ERR
	XRA	A		CHAN 0
	MVI	B,-1		WANT IT ALL
	MOV	C,A		EVEN SECTORS
	LXI	D,BUF		WHERE ..
	SCALL	.READ
	CPI	1		SHOULD HAVE EOF
	MVI	A,EC.NEM	PRETEND NOT ENOUGH RAM
	JNZ	ERR		NO??
	LHLD	BUF		GET FILE TYPE WORD
	MOV	A,L
	INR	A
	ORA	H		IF .ABS OR .TMI THEN 0
	MVI	A,EC.IFC	ILLEGAL FILE CONTENTS
	JNZ	ERR		FAKE ERROR
	CALL	$TYPTX		SAY GOODBYE ..
	DB	'HDOS environment is terminated.',12Q,12Q,212Q
TRICKY	MVI	A,200Q		WILL KEEP UO.HLT
	STA	.MFLAG		KILL HDOS CLOCK
	OUT	177Q		TURN DISK MOTORS OFF ALSO
	LHLD	S.HIMEM		UPSTAIRS, WHERE HDOS LIVES ..
	SPHL			.. AND NOW DIES
	MVI	A,20		WAIT 40 MS
	CALL	DLY		FOR USART TO SETTLE
	MVI	A,201Q		INSURE OUT OF MODE-SET
	OUT	373Q
	OUT	373Q
	MVI	A,100Q		LEAVE TT: IN MODE-SET INCASE PROGRAM
	OUT	373Q		 IS TOO STUPID TO DO IT ITSELF
	LHLD	BUF+6		GET ENTRY ADRESS
	PUSH	H		SET PC ON NEW STACK
	LHLD	BUF+4		GET # OF BYTES
	XCHG
	LHLD	BUF+2		GET FIRST ADR
	SHLD	START		SAVE FOR POSSIBLE DUMP
	DAD	D		COMPUTE LAST ADR
	DCX	H		..
	SHLD	ABUSS		SAVE FOR DUMP, ALSO
	LDA	STFLG		GET START UP FLAG
	ANA	A		TEST IT
	JZ	MOVIT1		WILL START PGM IF 0
	JMP	MOVIT2

* SMBAKER - COPY XCON8 TO 000H. THIS ASSUMES THAT
*   ROMDIS IS ENABLED AND THE ROM IS LIVING IN RAM
*   WE IMPLEMENT THIS TWICE, ONCE FOR AUTOMATIC
*   START (MOVIT1) AND ONCE FOR NON-AUTOMATIC-START
*   (MOVIT2).

MOVIT1  EQU	*
	DI			COPY FROM XCON8 TO 0000H
	LXI	H,0000H		DEST=0000
	LXI	D,XCON8		SRC=XCON8
	LXI	B,0800H		2KB
MOVLP1  LDAX	D
	MOV	M,A
	INX	D
	INX	H
	DCX	B
	MOV	A,B
	ORA	C
	JNZ	MOVLP1
	EI
	JMP     MOVIT3

* FOR NON-AUTOMATIC-START...

MOVIT2	EQU	*
	DI			COPY FROM XCON8 TO 0000H
	LXI	H,0000H		DEST=0000
	LXI	D,XCON8		SRC=XCON8
	LXI	B,0800H		2KB
MOVLP2  LDAX	D
	MOV	M,A
	INX	D
	INX	H
	DCX	B
	MOV	A,B
	ORA	C
	JNZ	MOVLP2
	EI			NOW FALL THROUGH...

	DI			SO PAM8 WON'T USE REGPTR
	CALL	SAVALL		SET UP FAKE REGISTERS
	LXI	H,ERROR		WILL GO TO PAM8 & BEEP
	PUSH	H		PUT ON NEW STACK
MOVIT3	LHLD	BUF+4		GET BYTE COUNT
	MOV	B,H
	MOV	C,L		.. INTO (BC)
	LXI	D,BUF+8		WHERE PROGRAM IS
	LHLD	BUF+2		WHERE IT WANTS TO BE
	JMP	$MOVE		LET H17 ROM MOVE IT, AND RETURN TO IT

RBAD	MOV	B,A		SAVE ERROR CODE
	XRA	A
	STA	STFLG		RESET START CONDITIONS
	MOV	A,B		GET ERROR
	CPI	EC.IFN		ONLY NAME ERROR CAN BE IGNORED
	JNZ	RBAD1		MUST ERROR
	LDA	LINFLG
	ANA	A		SEE IF WEVE BEEN HERE BEFORE
	MOV	A,B		GET CODE BACK
	JZ	NOERR		FIRST TIME
RBAD1	MVI	H,12Q
	SCALL	.ERROR		TELL ERROR
NOERR	MVI	A,-1		FLAG DONE
	STA	LINFLG
	SCALL	.CLRCO		NO TYPE-IN YET
	LXI	B,NAME		WHERE NAME GOES
	CALL	$TYPTX
	DB	'File name?',240Q
INLIN	SCALL	.SCIN
	JC	INLIN		WAIT FOR CHRS
	CPI	4		^D?
	JZ	QUIT		YES, DONE
	CPI	12Q		NL?
	JZ	GOTNAM		PROCESS IT
	CPI	' '		SPACE?
	JZ	INLIN		IGNORE
	CPI	'*'		ASTER?
	JNZ	RSTOR		NO, SAVE IT
	MVI	A,-1		SET FLAG
	STA	STFLG		SO NOT TO START
	JMP	INLIN

RSTOR	CPI	140Q		LC?
	JC	RS1		NO
	SUI	40Q		YES, CONVERT TO UPPER
RS1	STAX	B		SAVE THE CHR
	INX	B
	JMP	INLIN		GET MORE

LINFLG	DB	0		AFTER FIRST LINE WAS READ

ERR	MVI	H,12Q		NL AFTER MSG ..
	SCALL	.ERROR		PRINT IT
QUIT	XRA	A
	SCALL	.EXIT		AND QUIT

STFLG	DB	0		'WHERE TO START' FLAG

XCON8	EQU	*
	XTEXT	XCON8.ACM

DEFALT	DB	'SY0TMI'
NAME	EQU	*		SHARED BUFFER SPACE
BUF	EQU	*

VER	DB	12Q,12Q,'TMIloader Version 1.2 - Release 79.11.21',12Q,12Q

	END	ST
