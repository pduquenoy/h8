/* NOTE: When programming using XGPRO/MINIPRO make sure
 *       to specify ATF22V10C(UES). The "UES" is important.
 *
 * v0.12
 *   - Eliminated !AS on pin 5 and replaced with A7
 */

name bus;
device g22v10;
partno x;
date 1/1/1980;
designer smbaker;
company sbsoftware;
revision 1;
assembly x;
location x;

PIN 1 = !RESET;
PIN 2 = !WAITING;  /* OP1 */
PIN 3 = OP2;
PIN 4 = !CSIN;
PIN 5 = BM1;
PIN 6 = !SACK;
PIN 7 = !XACK;
PIN 8 = DEL1;
PIN 9 = DELAY; /* DEL2 */
PIN 10 = !BMEMW;
PIN 11 = !BMEMR;
PIN 12 = GND;
PIN 13 = NC1;
PIN 14 = TMP;
PIN 15 = REFRQ;
PIN 16 = OLATCH;
PIN 17 = !LOE;
PIN 18 = !CS;
PIN 19 = !WAIT;
PIN 20 = STARTDELAY; /* D2 */
PIN 21 = STARTWAIT;  /* D1 */
PIN 22 = !MW;
PIN 23 = !MR;

/* connect:
     STARTWAIT to 1CLK
     XACK to 1SET
     +5V (or !RESET) to 1CLR
     GND to 1D
     WAITING to 1Q
     -
     STARTDELAY to D2
     DELAY to DEL2
     -
     10pF / 5.1K on LS123
     20MHz crystal on D8203
     3.3k OR 5.1k resustor same result
     brutprim 100 on 2 MHz Z80: 43 seconds, vs 34 seconds on SRAM board
     -
     10pF / 3.3K on LS123
     24MHz crystal on D8203
     brutprim 100 37 seconds
     unreliable in 8085, probably due to 3.3K resistor
     -
     10pF / 3.3K on LS123
     25 MHz crystal on D8203
     brutprim 100 36.5 seconds
     unreliable in 8085, probably due to 3.3K resistor
*/

MW = !RESET & ((CS & BMEMW & !XACK & !MW & !MR & !DELAY) # (MW & BMEMW) # (MW & WAITING));
MR = !RESET & ((CS & BMEMR & !XACK & !MW & !MR & !DELAY) # (MR & BMEMR) # (MR & WAITING));

STARTDELAY = !RESET & XACK;

STARTWAIT = !RESET & (MW # MR);

WAIT = !RESET & (WAITING & (MW # MR));

CS = CSIN # WAITING; /* hold CS while we're waiting */

LOE = !RESET & CSIN & BMEMR;
OLATCH = XACK;
