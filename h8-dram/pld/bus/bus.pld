/* NOTE: When programming using XGPRO/MINIPRO make sure
 *       to specify ATF22V10C(UES). The "UES" is important.
 */

name bus;
device g22v10;
partno x;
date 1/1/1980;
designer smbaker;
company sbsoftware;
revision 1;
assembly x;
location x;

PIN 1 = !RESET;
PIN 2 = !WAITING;  /* OP1 */
PIN 3 = OP2;
PIN 4 = !CSIN;
PIN 5 = BM1;
PIN 6 = !SACK;
PIN 7 = !XACK;
PIN 8 = DEL1;
PIN 9 = DELAY;
PIN 10 = !BMEMW;
PIN 11 = !BMEMR;
PIN 12 = GND;
PIN 13 = NC1;
PIN 14 = TMP;
PIN 15 = REFRQ;
PIN 16 = OLATCH;
PIN 17 = !LOE;
PIN 18 = !CS;
PIN 19 = !WAIT;
PIN 20 = STARTDELAY; /* D2 */
PIN 21 = STARTWAIT;  /* D1 */
PIN 22 = !MW;
PIN 23 = !MR;

/* connect:
     STARTWAIT to 1CLK
     XACK to 1SET
     +5V to 1CLR
     GND to 1D
     WAITING to 1Q
     -
     !RESET to !RESET
*/

/* START MW on a BMEMW; hold it so long as we're WAITING */

MW = !RESET & ((CS & BMEMW & !XACK & !MW & !MR & !DELAY) # (MW & BMEMW) # (MW & WAITING));
MR = !RESET & ((CS & BMEMR & !XACK & !MW & !MR & !DELAY) # (MR & BMEMR) # (MR & WAITING));

STARTDELAY = XACK;
TMP = !MW & !MR & !XACK;  /* some additional protection in case delay hasn't caught up yet */

STARTWAIT = MW # MR;

WAIT = (WAITING & (MW # MR));

CS = CSIN # WAITING; /* hold CS while we're waiting */

LOE = CSIN & BMEMR;
OLATCH = XACK;
