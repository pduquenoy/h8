/* NOTE: When programming using XGPRO/MINIPRO make sure
 *       to specify ATF22V10C(UES). The "UES" is important.
 *
 * v0.12
 *   - Eliminated !AS on pin 5 and replaced with A7
 */

name bus;
device g22v10;
partno x;
date 1/1/1980;
designer smbaker;
company sbsoftware;
revision 1;
assembly x;
location x;

PIN 1 = !RESET;
PIN 2 = !WAITING;  /* OP1 */
PIN 3 = OP2;
PIN 4 = CSIN;
PIN 5 = BM1;
PIN 6 = !SACK;
PIN 7 = !XACK;
PIN 8 = DEL1;
PIN 9 = DEL2;
PIN 10 = BMEMW;
PIN 11 = BMEMR;
PIN 12 = GND;
PIN 13 = NC1;
PIN 14 = !TMP;
PIN 15 = REFRQ;
PIN 16 = OLATCH;
PIN 17 = !LOE;
PIN 18 = !CS;
PIN 19 = !WAIT;
PIN 20 = LASTWAIT; /* D2 */
PIN 21 = STARTWAIT; /* D1 */
PIN 22 = !MW;
PIN 23 = !MR;

/* connect:
     STARTWAIT to 1CLK
     XACK to 1SET
     +5V to 1CLR
     GND to 1D
     WAITING to 1Q
     -
     !RESET to !RESET
*/

/* START MW on a BMEMW; hold it so long as we're WAITING */

MW = (CS & BMEMW & TMP & !RESET) # (MW & WAITING & !RESET);
MR = (CS & BMEMR & TMP & !RESET) # (MR & WAITING & !RESET);

/* tmp is our deglitcher. We can use it to get the state of BMEMW and BMEMR a few ns into the past. */

TMP = (BMEMW # BMEMR) & !RESET;

/* STARTWAIT will clock in a 0 to WAITING */

STARTWAIT = CS & (BMEMW # BMEMR);

/* WAITING will clear when XACK is asserted.
   WAIT follows WAITING, qualified by (MW or MR), since we don't want to wait if we're not doing anything.
*/

WAIT = LASTWAIT # (WAITING & (MW # MR));
LASTWAIT = WAITING & (MW # MR);

CS = CSIN # WAITING; /* hold CS while we're waiting */

LOE = CSIN & BMEMR;
OLATCH = XACK;
