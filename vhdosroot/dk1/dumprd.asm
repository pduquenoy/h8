
	TITLE	'DUMP - DUMP RAMDISK CONTENTS'
	EJECT
***	DUMP - DUMP RAMDISK CONTENSTS
*
*       SMBAKER


	XTEXT	HOSDEF
	XTEXT	HOSEQU
	XTEXT   PGREG
**	ORG	USERFWA

**ENTRY	JMP	MAIN
**
	ORG	04000H		

ENTRY	LXI	HJ,INTRO
	SCALL	.PRINT
	CALL	PGINIT

	MVI	A,00H
	OUT	WR00K,A		Select Page 0

	MVI	H,00H
	MVI	L,00H

	MVI	B,0		Output 256 rows

ROWLOOP	CALL	PHEXHL
	MVI	A,' '
	SCALL	.SCOUT

	PUSH	H		Save HL so we can repeat with printing the chars
	MVI	C,16
COLLOOP	DI
	MVI	A,083H
	OUT	RD00KH,A
	MOV	D,M		Load byte in (HL) into D
	MVI	A,000H
	OUT	RD00KH,A
	EI

	MOV	A,D
	CALL	PHEXA
	MVI	A,' '
	SCALL	.SCOUT

	INX	H

	DCR	C
	JNZ	COLLOOP
	POP	H

	MVI	C,16
PRNLP	DI
	MVI	A,083H
	OUT	RD00KH,A
	MOV	D,M		Load byte in (HL) into D
	MVI	A,000H
	OUT	RD00KH,A
	EI

	MOV	A,D
	CPI	020H
	JC	NOTPR
	CPI	07FH
	JNC	NOTPR
	JMP	DOPR
NOTPR	MVI	A,'.'
DOPR	SCALL	.SCOUT
	INX	H
	DCR	C
	JNZ	PRNLP

	MVI	A,012Q
	SCALL	.SCOUT

	DCR	B
	JNZ	ROWLOOP

	XRA	A
	SCALL	.EXIT

PHEXA	PUSH	PSW
	RLC
	RLC
	RLC
	RLC
	CALL	PHEXA0
	POP	PSW
PHEXA0	ANI	00FH
	ADI	090H
	DAA
	ACI	040H
	DAA
	SCALL    .SCOUT
	RET

PHEXHL	PUSH	PSW
	MOV	A,H
	CALL	PHEXA
	MOV	A,L
	CALL	PHEXA
	POP	PSW
	RET

PHEXDE	PUSH	PSW
	MOV	A,D
	CALL	PHEXA
	MOV	A,E
	CALL	PHEXA
	POP	PSW
	RET

PHEXBC	PUSH	PSW
	MOV	A,B
	CALL	PHEXA
	MOV	A,C
	CALL	PHEXA
	POP	PSW
	RET

	XTEXT	PGMAP

INTRO	DB	12Q,'RAMDISK DUMP UTILITY',212Q

	LON	G
	END	ENTRY
