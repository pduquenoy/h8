Name intreg;
Partno G22V10#1;
Revision;
Date 09/1/2023;
Designer Scott Baker;
Company ;
Location ;
Assembly 8008 H8 CPU BOARD;
Device G22V10;

/* Note that all inputs on GAL22V10D have weak pullups */

/* Inputs: */
Pin 1 = !INTPOLL;
Pin 2 = !INT0;  /* 000 - jumps to 0x00 - not implemented here */
Pin 3 = !INT1;  /* 001 - jumps to 0x08 */
Pin 4 = !INT2;  /* 010 - jumps to 0x10 */
Pin 5 = !INT3;  /* 011 - jumps to 0x18 */
Pin 6 = !INT4;  /* 100 - jumps to 0x20 */
Pin 7 = !INT5;  /* 101 - jumps to 0x28 */
Pin 8 = !INT6;  /* 110 - jumps to 0x30 */
Pin 9 = !INT7;  /* 111 - jumps to 0x38*/
Pin 10 = SIMFPINT;
Pin 11 = POR;
Pin 13 = !INTJAM;

/* Outputs:   */
Pin 23 = D0;
Pin 22 = D1;
Pin 21 = D2;
Pin 20 = D3;
Pin 19 = D4;
Pin 18 = D5;
Pin 17 = D6;
Pin 16 = D7;
Pin 15 = !INTREQ;
Pin 14 = OUTEN;

FIELD DATA = [D7..D0];

INTREQ = INT1; /* for now, only pay attention to int1 */

/* There are two cases where we want to put data out on the bus,
 * when INTPOLL is true or when INTJAM is true. The OE for each
 * data pin can't take that complex of a term, so we use a
 * temporary.
 */

OUTEN = INTPOLL # INTJAM;

D0.OE = OUTEN;
D1.OE = OUTEN;
D2.OE = OUTEN;
D3.OE = OUTEN;
D4.OE = OUTEN;
D5.OE = OUTEN;
D6.OE = OUTEN;
D7.OE = OUTEN;

/* Two Modes:
 *
 *  INTJAM
 *     D7..D0 contain an RST instruction, and applies the highest
 *     numbered interrupt as priority over lesser numbered interrupts.
 *
 *  INTPOLL
 *     D7..D0 contain the values of INT7..INT0.
 *
 *  Whether INTPOLL or INTJAM are asserted depends on which of these
 *  modes are executed. INTPOLL and INTJAM are also used (see OUTEN)
 *  to enable D7..D0. 
 *
 *  Note that the equations below can be simplified, and are automatically
 *  optimized by wincupl.
 */

D0 = (INTPOLL & INT0) # (INTJAM & 'b'1);
D1 = (INTPOLL & INT1) # (INTJAM & 'b'0);
D2 = (INTPOLL & INT2) # (INTJAM & 'b'1);
D3 = (INTPOLL & INT3) # (INTJAM & (
        (INT7) #
        (INT5 & !INT6 & !INT7) #
        (INT3 & !INT4 & !INT5 & !INT6 & !INT7) #
        (INT1 & !INT2 & !INT3 & !INT4 & !INT5 & !INT6 & !INT7)
     ));
D4 = (INTPOLL & INT4) # (INTJAM & (
        (INT7) #
        (INT6 & !INT7) #
        (INT3 & !INT4 & !INT5 & !INT6 & !INT7) #
        (INT2 & !INT3 & !INT4 & !INT5 & !INT6 & !INT7)
     ));
D5 = (INTPOLL & INT5) # (INTJAM & (
        (INT7) #
        (INT6 & !INT7) #
        (INT5 & !INT6 & !INT7) #
        (INT4 & !INT5 & !INT6 & !INT7)
     ));
D6 = (INTPOLL & INT6) # (INTJAM & 'b'0);
D7 = (INTPOLL & INT7) # (INTJAM & 'b'0);


/* this was when I had the priority backwards...
D3 = INT1 # 
     (INT3 & !INT2 & !INT1) #
     (INT5 & !INT4 & !INT3 & !INT2 & !INT1) #
     (INT7 & !INT6 & !INT5 & !INT4 & !INT3 & !INT2 & !INT1);
D4 = (INT2 & !INT1) # 
     (INT3 & !INT2 & !INT1) # 
     (INT6 & !INT5 & !INT4 & !INT3 & !INT2 & !INT1) #
     (INT7 & !INT6 & !INT5 & !INT4 & !INT3 & !INT2 & !INT1);
D5 = (INT4 & !INT3 & !INT2 & !INT1) #
     (INT5 & !INT4 & !INT3 & !INT2 & !INT1) #
     (INT6 & !INT5 & !INT4 & !INT3 & !INT2 & !INT1) #
     (INT7 & !INT6 & !INT5 & !INT4 & !INT3 & !INT2 & !INT1);
*/
