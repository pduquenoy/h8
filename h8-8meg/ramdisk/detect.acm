** PGINIT initializes all page registers but does not enable paging

TESTOFS	EQU	0000H

DETECT	EQU	*
	LXI	DE,TESTOFS

	DI
	LDAX	D
	STA	SAVEM		SAVE BANK0:PAGE0:TESTOFS INTO SAVEM
	MVI	A,085H
	OUT	RD00K		ENABLE PAGING, SELECT PAGE 5 IN BANK 0
	LDAX	D
	STA	SAVE0		SAVE BANK0:PAGE5:TESTOFS INTO SAVE0

	MVI	A,0A5H		SELECT PAGE37
	OUT	RD00K
	LDAX	D
	STA	SAVE037		SAVE BANK0:PAGE37:TESTOFS INTO SAVE037

	MVI	A,80H
	OUT	RD00K		RESTORE PAGE REGS TO BANK0:PAGE0:TESTOFS
	STAX	D		WRITE 080H TO BANK0:PAGE0:TESTOFS

	MVI	A,85H
	OUT	WR00K
	STAX	D		WRITE 085H TO BANK0:PAGE5:TESTOFS

	MVI	A,80H
	OUT	RD00K
	OUT	WR00K		MAP PAGE0
	LDAX	D
	CPI	080H
	JZ	DETECT1
	MVI	A,001H		BANK0:PAGE0:TESTOFS DID NOT CONTAIN 80H
	STA	DETNMMU
	JMP	DETDONE

DETECT1	MVI	A,085H
	OUT	RD00K
	OUT	WR00K
	LDAX	D
	CPI	085H
	JZ	DETECT2
	MVI	A,001H		BANK0:PAGE5:TESTOFS DID NOT CONTAIN 85H
	STA	DETNMMU
	JMP	DETDONE

DETECT2	MVI	A,01H
	STA	DETRAML		WE HAVE DETECTED RD0
	MVI	A,0A5H
	OUT	RD00K		SELECT PAGE37
	OUT	WR00K
	STAX	D		WRITE BANK0:PAGE37:TESTOFS
	LDAX	D		READ IT BACK
	CPI	0A5H		NO CHIP INSTALLED?
	JZ	DETECT3
	MVI	A,001H
	STA	DETTINY		... FALL THROUGH

DETECT3	MVI	A,085H
	OUT	RD00K		SELECT BANK0:PAGE5
	OUT	WR00K
	LDAX	D
	CPI	085H		DID BANK0:PAGE5:TESTOFS CHANGE?
	JZ	DETECT4
	MVI	A,001H
	STA	DET512		WE HAVE A 512K BOARD
	STA	DETTINY		... AND IT'S TINY BY NATURE
	JMP	DETDONE

* AT THIS POINT, WE SHOWED THAT WE WERE ABLE TO MAP PAGE37
* THEREFORE WE MUST HAVE AN 8MB BOARD. 512K BOARD CAN ONLY
* MAP PAGES 0-31.

DETECT4	EQU	*

* NOW CHECK BANKS 1-7 TO SEE IF RAM IS INSTALLED

	MVI	A,085H
	OUT	RD00K		MAP PAGE5 FOR READ AND WRITE
	OUT	WR00K
	MVI	B,002H		B HOLDS BITMASK
	MVI	C,081H		C HOLDS BANK NUMBER

CKLOOP	MOV	A,C
	OUT	RD00KH
	OUT	WR00KH
	LDAX	D		GET EXISTING MEMORY VALUE
	STA	SAVEB		--- AND SAVE IT FOR LATER
	MVI	A,033H		USE 033H AS A PATTERN
	STAX	D		STORE THE PATTERN
	LDAX	D		READ THE PATTERN BACK
	CPI	033H
	JNZ	CKLOOP1		PATTERN DID NOT MATCH - NO RAM - JUMP OVER
	LDA	DETRAML
	ORA	B
	STA	DETRAML
CKLOOP1	LXI	H,CKIR1		RETURN ADDRESS IN HL
	JMP	ISFLASH		WE CANNOT DO CALL WHILE MAPPED
CKIR1	JNC	CKLOOP2
	LDA	DETFLSL
	ORA	B
	STA	DETFLSL
CKLOOP2	LDA	SAVEB
	STAX	D		RESTORE THE ORIGINAL DATA
	MOV	A,B
	RLC
	MOV	B,A
	INR	C
	MOV	A,C
	CPI	088H		STOP AT 8TH DEVICE
	JNZ	CKLOOP		LOOP THROUGH ALL 8 DEVICES

* NOW CHECK EBANKS 8-15 TO SEE IF RAM IS INSTALLED

	MVI	B,001H		RESET THE BITMASK FOR DETRAMH
CKLOOP3	MOV	A,C
	OUT	RD00KH
	OUT	WR00KH
	LDAX	D		GET EXISTING MEMORY VALUE
	STA	SAVEB		--- AND SAVE IT FOR LATER
	MVI	A,033H		USE 033H AS A PATTERN
	STAX	D		STORE THE PATTERN
	LDAX	D		READ THE PATTERN BACK
	CPI	033H
	JNZ	CKLOOP4		PATTERN DID NOT MATCH - NO RAM - JUMP OVER
	LDA	DETRAMH
	ORA	B
	STA	DETRAMH
CKLOOP4	LXI	H,CKIR2		RETURN ADDRESS IN HL
	JMP	ISFLASH		WE CANNOT DO CALL WHILE MAPPED
CKIR2	JNC	CKLOOP5
	LDA	DETFLSH
	ORA	B
	STA	DETFLSH
CKLOOP5	LDA	SAVEB
	STAX	D		RESTORE THE ORIGINAL DATA
	MOV	A,B
	RLC
	MOV	B,A
	INR	C
	MOV	A,C
	CPI	090H		STOP AT 16th DEVICE
	JNZ	CKLOOP3		LOOP THROUGH ALL 8 DEVICES

* CLEAN UP

DETDONE	EQU	*
	LDA	DETRAML		DETDEVL = DETRAML | DETFLSL
	MOV	B,A
	LDA	DETFLSL
	ORA	B
	STA	DETDEVL

	LDA	DETRAMH		DETDEVH = DETRAMH | DETFLSH
	MOV	B,A
	LDA	DETFLSH
	ORA	B
	STA	DETDEVH

	MVI	A,080H
	OUT	WR00KH		RESTORE BANK SELECTION REGS TO BANK0
	OUT	RD00KH
	MVI	A,0A5H		SELECT PAGE37
	OUT	WR00K
	LDA	SAVE037
	STAX	D		RESTORE SAVE037 TO BANK0:PAGE37:TESTOFS

	MVI	A,085H
	OUT	WR00K
	LDA	SAVE0
	STAX	D		RESTORE SAVE0 TO BANK0:PAGE5:TESTOFS

* ALWAYS SET BANK0:PAGE0 LAST. THIS COVERS THE CASE WHERE NO MMU IS
* INSTALLED, AND WE HAVE TO REPAIR MAIN MEMORY.

	MVI	A,080H
	OUT	WR00K
	LDA	SAVEM
	STAX	D		RESTORE SAVEM TO BANK0:PAGE0:TESTOFS

	MVI	A,000H		RESTORE PAGE REGS AND DISABLE PAGING
	OUT	WR00K
	OUT	RD00K
	OUT	WR00KH
	OUT	RD00KH
	EI

	RET

* ISFLASH
* Return with carry set if bank contains flash, carry clear otherwise
* Warning: if used on writeable device, would write to memory
* Returns with
*    Page regs retored to page 5
* Destroys
*    A

ISFLASH EQU	*
	MVI	A,081H		PAGE1
	OUT	WR00K
	MVI	A,0AAH
	STA	01555H		[0x5555] = 0xAA		CHIP ID SEQUENCE
	MVI	A,080H		PAGE0
	OUT	WR00K
	MVI	A,055H
	STA	02AAAH		[0x2AAA] = 0x55
	MVI	A,081H		PAGE1
	OUT	WR00K,A
	MVI	A,090H
	STA	01555H		[0x5555] = 0x90
	MVI	A,080H		PAGE0
	OUT	RD00K
	OUT	WR00K
	LDA	00000H		LOAD ADDRESS 0
	CPI	0BFH
	JNZ	NOTFLAS
	LDA	00001H		LOAD ADDRESS 1
	CPI	0B7H
	JNZ	NOTFLAS
	STC			SET CARRY
	JMP	ISFOUT
NOTFLAS	ANA	A		CLEAR CARRY
ISFOUT	MVI	A,0F0H		EXIT CHIP ID...
	STA	0000H		...BY WRITING F0 to ANY ADDRESS
	MVI	A,085H
	OUT	RD00K		RETURN PAGE REGS BACK TO PAGE 5
	OUT	WR00K
	PCHL			JUMP TO HL

SAVEM	DB	0
SAVE0	DB	0
SAVE037	DB	0
SAVEB 	DB	0

DETNMMU	DB	0		SET TO 1 IF MMU BOARD IS PRESENT
DETTINY	DB	0		SET TO 1 IF BOARD ONLY CONTAINS 512K
DET512	DB	0		SET TO 1 IF BANK0 IS 512K BOARD

DETRAM	EQU	*		16 BITS, SET TO 1 IF BANK IS RAM
DETRAML	DB	0
DETRAMH	DB	0
DETFLS	EQU	*		16 BITS, SET TO 1 IF BANK IS FLASH
DETFLSL	DB	0
DETFLSH DB	0
DETDEV	EQU	*		16 BITS, SET TO 1 IF BANK IS RAM OR FLASH
DETDEVL	DB	0
DETDEVH DB	0



