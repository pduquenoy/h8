** PGINIT initializes all page registers but does not enable paging

TESTOFS	EQU	0000H

DETECT	EQU	*
	LXI	DE,TESTOFS

	DI
	LDAX	D
	STA	SAVEM		SAVE BANK0:PAGE0:TESTOFS INTO SAVEM
	MVI	A,085H
	OUT	RD00K		ENABLE PAGING, SELECT PAGE 5 IN BANK 0
	LDAX	D
	STA	SAVE0		SAVE BANK0:PAGE5:TESTOFS INTO SAVE0

	MVI	A,0A5H		SELECT PAGE37
	OUT	RD00K
	LDAX	D
	STA	SAVE037		SAVE BANK0:PAGE37:TESTOFS INTO SAVE037

	MVI	A,80H
	OUT	RD00K		RESTORE PAGE REGS TO BANK0:PAGE0:TESTOFS
	STAX	D		WRITE 080H TO BANK0:PAGE0:TESTOFS

	MVI	A,85H
	OUT	WR00K
	STAX	D		WRITE 085H TO BANK0:PAGE5:TESTOFS

	MVI	A,80H
	OUT	RD00K
	OUT	WR00K		MAP PAGE0
	LDAX	D
	CPI	080H
	JZ	DETECT1
	MVI	A,001H		BANK0:PAGE0:TESTOFS DID NOT CONTAIN 80H
	STA	DETNMMU
	JMP	DETDONE

DETECT1	MVI	A,085H
	OUT	RD00K
	OUT	WR00K
	LDAX	D
	CPI	085H
	JZ	DETECT2
	MVI	A,001H		BANK0:PAGE5:TESTOFS DID NOT CONTAIN 85H
	STA	DETNMMU
	JMP	DETDONE

DETECT2	MVI	A,01H
	STA	DETDEV		WE HAVE DETECTED RD0
	MVI	A,0A5H
	OUT	RD00K		SELECT PAGE37
	OUT	WR00K
	STAX	D		WRITE BANK0:PAGE37:TESTOFS
	LDAX	D		READ IT BACK
	CPI	0A5H		NO CHIP INSTALLED?
	JZ	DETECT3
	MVI	A,001H
	STA	DETTINY		... FALL THROUGH

DETECT3	MVI	A,085H
	OUT	RD00K		SELECT BANK0:PAGE5
	OUT	WR00K
	LDAX	D
	CPI	085H		DID BANK0:PAGE5:TESTOFS CHANGE?
	JZ	DETECT4
	MVI	A,001H
	STA	DET512		WE HAVE A 512K BOARD
	STA	DETTINY		... AND IT'S TINY BY NATURE
	JMP	DETDONE

* AT THIS POINT, WE SHOWED THAT WE WERE ABLE TO MAP PAGE37
* THEREFORE WE MUST HAVE AN 8MB BOARD. 512K BOARD CAN ONLY
* MAP PAGES 0-31.

DETECT4	EQU	*

* NOW CHECK EACH BANK TO SEE IF RAM IS INSTALLED

	MVI	A,085H
	OUT	RD00K		MAP PAGE5 FOR READ AND WRITE
	OUT	WR00K
	MVI	B,002H		B HOLDS BITMASK
	MVI	C,081H		C HOLDS BANK NUMBER

CKLOOP	MOV	A,C
	OUT	RD00KH
	OUT	WR00KH
	LDAX	D		GET EXISTING MEMORY VALUE
	STA	SAVEB		--- AND SAVE IT FOR LATER
	MVI	A,033H		USE 033H AS A PATTERN
	STAX	D		STORE THE PATTERN
	LDAX	D		READ THE PATTERN BACK
	CPI	033H
	JNZ	CKLOOP1		PATTERN DID NOT MATCH - NO RAM - JUMP OVER
	LDA	DETDEV
	ORA	B
	STA	DETDEV
CKLOOP1	LDA	SAVEB
	STAX	D		RESTORE THE ORIGINAL DATA
	MOV	A,B
	RLC
	MOV	B,A
	INR	C
	MOV	A,C
	CPI	088H
	JNZ	CKLOOP		LOOP THROUGH ALL 8 DEVICES

* CLEAN UP

DETDONE	EQU	*
	MVI	A,080H
	OUT	WR00KH		RESTORE BANK SELECTION REGS TO BANK0
	OUT	RD00KH
	MVI	A,0A5H		SELECT PAGE37
	OUT	WR00K
	LDA	SAVE037
	STAX	D		RESTORE SAVE037 TO BANK0:PAGE37:TESTOFS

	MVI	A,085H
	OUT	WR00K
	LDA	SAVE0
	STAX	D		RESTORE SAVE0 TO BANK0:PAGE5:TESTOFS

* ALWAYS SET BANK0:PAGE0 LAST. THIS COVERS THE CASE WHERE NO MMU IS
* INSTALLED, AND WE HAVE TO REPAIR MAIN MEMORY.

	MVI	A,080H
	OUT	WR00K
	LDA	SAVEM
	STAX	D		RESTORE SAVEM TO BANK0:PAGE0:TESTOFS

	MVI	A,000H		RESTORE PAGE REGS AND DISABLE PAGING
	OUT	WR00K
	OUT	RD00K
	OUT	WR00KH
	OUT	RD00KH
	EI

	RET

SAVEM	DB	0
SAVE0	DB	0
SAVE037	DB	0
SAVEB 	DB	0

DETNMMU	DB	0
DETTINY	DB	0
DET512	DB	0
DETDEV	DB	0

