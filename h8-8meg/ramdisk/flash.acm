** FERASE - ERASE FLASH
*
* Input
*   HL = offset within 16K
*   C = Bank
*   D = 16K Page Number
* Destroys
*   A
*   B
*   DE
*   HL

FERASE	EQU	*
	DI
	MOV	A,D
	ANI	0E0H
	ORI	081H		A=(D&0xE0)|0x81 == PAGE1
	OUT	WR00K,A
	MOV	A,C
	ORI	080H
	OUT	WR00KH,A	SELECT BANK in C
	OUT	RD00KH,A	... FOR READ ALSO
	SHLD	FERTMP
	LXI	H,01555H	HL = 0x5555 - 0x4000H
	MVI	A,0AAH
	MOV	M,A		[0x5555] = 0xAA
	MOV	A,D
	ANI	0E0H
	ORI	080H		A=(D&0xE0)|0x80 == PAGE0
	OUT	WR00K,A
	LXI	H,02AAAH
	MVI	A,055H
	MOV	M,A		[0x2AAA] = 0x55
	MOV	A,D
	ANI	0E0H
	ORI	081H		A=(D&0xE0)|0x81 == PAGE1
	OUT	WR00K,A
	LXI	H,01555H	HL = 0x5555 - 0x4000H
	MVI	A,080H
	MOV	M,A		[0x5555] = 0x80
	MVI	A,0AAH
	MOV	M,A		[0x5555] = 0xAA
	MOV	A,D
	ANI	0E0H
	ORI	080H		A=(D&0xE0)|0x80 == PAGE0
	OUT	WR00K,A
	LXI	H,02AAAH
	MVI	A,055H		[0x2AAA] = 0x55
	MOV	M,A
	LHLD	FERTMP

	MOV	A,D
	ORI	080H
	OUT	WR00K,A
	OUT	RD00K,A		... FOR READ ALSO

	MVI	A,030H		dest_byte = 0x30

	MOV	M,A		WRITE THE BYTE
	MOV	B,M		READ IT BACK
FERWL	MOV	A,M		READ IT BACK AGAIN
	CMP	B
	JZ	FERWO		IF WE GOT BACK THE SAME THING, FLASH IS READY
	MOV	B,A
	JMP	FERWL		KEEP TRYING

FERWO	MVI	A,080H
	OUT	RD00KH
	OUT	WR00KH
	OUT	WR00K
	OUT	RD00K
	MVI	A,000H
	OUT	RD00K		TURN OFF PAGING
	EI

	RET

FERTMP	DW	0

** FWRITE - WRITE FLASH
*
* Input
*   HL = offset within 16K
*   A = BYTE
*   C = BANK
*   D = 16K page number
* Destroys
*   A
*   B
*   E

FWRITE	EQU	*
	DI

	MOV	E,A		SAVE BYTE IN E
	MOV	A,D
	ANI	0E0H
	ORI	081H		A=(D&0xE0)|0x81 == PAGE1
	OUT	WR00K,A
	MOV	A,C
	ORI	080H
	OUT	WR00KH,A	SELECT BANK in C
	OUT	RD00KH,A	... FOR READ ALSO
	SHLD	FWRTMP
	LXI	H,01555H	HL = 0x5555 - 0x4000H
	MVI	A,0AAH
	MOV	M,A		[0x5555] = 0xAA
	MOV	A,D
	ANI	0E0H
	ORI	080H		A=(D&0xE0)|0x80 == PAGE0
	OUT	WR00K,A
	LXI	H,02AAAH
	MVI	A,055H
	MOV	M,A		[0x2AAA] = 0x55
	MOV	A,D
	ANI	0E0H
	ORI	081H		A=(D&0xE0)|0x81 == PAGE1	
	OUT	WR00K,A
	LXI	H,01555H	HL = 0x5555 - 0x4000H
	MVI	A,0A0H
	MOV	M,A		[0x5555] = 0xA0
	LHLD	FWRTMP

	MOV	A,D
	ORI	080H
	OUT	WR00K,A
	OUT	RD00K,A		... FOR READ ALSO

	MOV	A,E		PUT BYTE TO WRITE IN E

	MOV	M,A		WRITE THE BYTE
	MOV	B,M		READ IT BACK
FWRL	MOV	A,M		READ IT BACK AGAIN
	CMP	B
	JZ	FWRO		IF WE GOT BACK THE SAME THING, FLASH IS READY
	MOV	B,A
	JMP	FWRL		KEEP TRYING

FWRO	MVI	A,080H		RESTORE PAGES
	OUT	RD00KH
	OUT	WR00KH
	OUT	WR00K
	OUT	RD00K
	MVI	A,000H
	OUT	RD00K		TURN OFF PAGING
	EI

	RET
FWRTMP	DW	0

** Input
*    HL = 4K block number
*  Output
*    D = 16K page number == (HL >> 2)
*    HL = offset == (HL << 12) & 0x3FFF
*  Destroys
*    A
*    B
FMAP	MOV	A,L
	ANI	0FCH
	RRC
	RRC			A = L >> 2
	MOV	B,A
	MOV	A,H
	ANI	003H
	RLC
	RLC
	RLC
	RLC
	RLC
	RLC			A = H << 6
	ORA	B		A = (H<<6) | (L>>2)
	MOV	D,A		PAGE NUMBER IN D

	MOV	A,L		
	ANI	003H
	RLC
	RLC
	RLC
	RLC
	MOV	H,A		HL = (H & 0x03) << 12
	MVI	L,000H
	RET
